import com.badlogic.gdx.tools.texturepacker.TexturePacker

apply plugin: "java"

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        jcenter()
    }
    dependencies {
        classpath "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
    }
}

dependencies {
    compile group: 'com.google.code.gson', name: 'gson', version: '2.7'
}

sourceCompatibility = 1.7
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

def generatedResources = "$buildDir/generated-resources"

sourceSets.main.java.srcDirs = ["src/"]
sourceSets {
    main {
        output.dir(generatedResources)
    }
}

task texturePacker {
    ext.srcDir = new File(projectDir, "textures")
    ext.dstDir = new File(generatedResources)
    inputs.dir srcDir
    outputs.dir dstDir
    doLast {
        TexturePacker.process(srcDir.getAbsolutePath(), dstDir.getAbsolutePath(), "textures")
    }
}
compileJava.dependsOn texturePacker

eclipse.project {
    name = appName + "-core"
}
